# IMPORTANT NOTE: Run each step as a separate query to FaunaDB!
#
# Why?
# Because step #2 depends on step #1
# and it's better to be organized!

# Step 1: Create a "users" collection
CreateCollection({ name: "users" });

# Step 2: Create "todos" collection
CreateCollection({ name: "todos" });

# Step 3: Create all relevant Indexes
CreateIndex({
  name: "users_by_email",
  source: Collection("users"),
  terms: [{ field: ["data", "email"] }],
  unique: true
});
CreateIndex({
  name: "all_todos",
  source: Collection("todos"),
});
CreateIndex({
  name: "todos_by_completed_state",
  source: Collection("todos"),
  terms: [{ field: ["data", "completed"] }],
});

# Step 4: Add ABAC role
CreateRole({
  name: "todo-user-role",
  membership: [
    {
      resource: Collection("users")
    }
  ],
  privileges: [
    {
      resource: Collection("todos"),
      actions: {
        read: Query(
          Lambda(
            "todoRef",
            Let(
              {
                todo: Get(Var("todoRef")),
                userRef: Select(["data", "user"], Var("todo"))
              },
              Equals(Var("userRef"), Identity())
            )
          )
        ),
        write: true,
        create: true,
        delete: true,
        history_read: false,
        history_write: false,
        unrestricted_read: false
      }
    },
    {
      resource: Index("all_todos"),
      actions: {
        unrestricted_read: false,
        read: true
      }
    },
    {
      resource: Index("todos_by_completed_state"),
      actions: {
        unrestricted_read: false,
        read: true
      }
    },
    {
      resource: Index("users_by_email"),
      actions: {
        unrestricted_read: false,
        read: true
      }
    }
  ]
})
